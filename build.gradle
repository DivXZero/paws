apply plugin: 'cpp'

task wrapper(type: Wrapper) {
  gradleVersion = '2.13'
}

model {

  platforms {
    windows_x86 {
      architecture 'x86'
      operatingSystem 'windows'
    } /* windows_x86 */
  } /* platforms */

  toolChains {
    visualCpp(VisualCpp)
  } /* toolChains */

  components {

    paws_app(NativeExecutableSpec) {
      sources {
        cpp {
          source {
            srcDir 'src/app'
          } /* source */
          lib library: "paws_core"
        } /* cpp */
      } /* sources */
      binaries.all {
        if(targetPlatform.operatingSystem.windows) {
          if(toolChain in VisualCpp) {
            cppCompiler.args '/EHsc'
            linker.args '-mwindows', '/SUBSYSTEM:windows', '/ENTRY:mainCRTStartup'
          }
        }
      } /* binaries */
    } /* paws_app */

    paws_core(NativeLibrarySpec) {
      sources {
        cpp {
          source {
            srcDir 'src/core'
          } /* source */
        } /* cpp */
      } /* sources */
    } /* paws_core */

    all {
      targetPlatform 'windows_x86'
      sources {
        cpp {
          exportedHeaders {
            srcDir 'src'
          }
        } /* cpp */
      } /* sources */
    } /* all */

  } /* components */
} /* model */
